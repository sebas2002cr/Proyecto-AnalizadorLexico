package syntaxis;

import java_cup.runtime.*;
import lexical.CodeLexer;
import semantic.*;

action code {:
	ArbolSintactico arbol = new ArbolSintactico();
:}

parser code {:
    CodeLexer lex;

	@SuppressWarnings("deprecation")
	public parser(CodeLexer lex) {
		this.lex = lex;
		this.symbolFactory = new DefaultSymbolFactory();
	}
:}

init with {: :};

scan with {: return lex.next_token(); :};
// operadores
terminal RUDOLPH,      // Operador de suma
        DASHER,       // Operador de resta
        COMET,        // Operador de multiplicación
        VIXEN,        // Operador de división
        TOTIN,        // Operador de módulo
        RENILLO,      // Operador de potencia
		GRINCH,       // Operador de incremento unario
        QUIEN,        // Operador de decremento unario
        KNECHT_RUPRECHT, // Operador relacional mayor o igual
        KRAMPUS,      // Operador relacional mayor
        BELSNICKEL,   // Operador relacional menor o igual
        ZWARTE_PIET,  // Operador relacional menor
        SNOWBALL_ALABASTRO, // Operador relacional diferente
        PEPPER_MINSTIX,     // Operador relacional igual
        MELCHOR,      // Operador lógico de conjunción
        GASPAR,       // Operador lógico de disyunción
        BALTASAR;     // Operador lógico de negación

// tipos
terminal PERSONA,       // Identificador
        PAPA_NOEL,      // Tipo INT
        SANTA_CLAUS,    // Tipo BOOLEAN
        SAN_NICOLAS,    // Tipo STRING
        NINO_JESUS,     // Tipo CHAR
		COLACHO,        // Tipo FLOAT
		SANTA;          // Tipo ARRAY

// literales
terminal L_COLACHO,      // Literal FLOAT
		 L_SAN_NICOLAS,  // Literal STRING
		 L_PAPA_NOEL,    // Literal INT
         L_SANTA_CLAUS; // Literal BOOLEAN

// símbolos
terminal ABRECUENTO,     // Símbolo "("
		CIERRECUENTO,   // Símbolo ")"
		ABREEMPAQUE,    // Símbolo "["
		CIERRAEMPAQUE,  // Símbolo "]"
		ABREREGALO,     // Símbolo "{"
		CIERRAREGALO,   // Símbolo "}"
		FIN_REGALO;     // Símbolo "|"

// PRINT - READ
terminal NARRA,           // Palabra clave PRINT
        ESCUCHA;         // Palabra clave READ

// estructuras
terminal ELFO,            // Palabra clave IF
        HADA,             // Palabra clave ELIF
        DUENDE,           // Palabra clave ELSE
        ENVUELVE,         // Palabra clave FOR
        HACE,             // Palabra clave DO
        REVISA,           // Palabra clave UNTIL
        ENVIA,            // Palabra clave RETURN
        CORTA;            // Palabra clave BREAK
terminal ENTREGA,         // Operador de asignación ":="
        COMA,             // Símbolo ","
        DOS_PUNTOS;       // Símbolo ":"
terminal FUNCTION,        // Palabra clave FUNCTION
        MAIN,            // Palabra clave MAIN
        LOCAL;           // Palabra clave LOCAL

terminal ERROR_IDENTIFICADOR,  // Terminal para errores en identificadores
        ERROR_LITERAL;        // Terminal para errores en literales

// no terminales
non terminal navidad, bolsa_navidenna, funcion, funcion_main, inicio;
non terminal parametros, parametro, tipo, array, mas_parametros;
non terminal asignar_variable, asignar_variable_declarada, declarar_asignar_variable;
non terminal bloque_codigo, linea_codigo, codigo_multilinea, codigo_contenido;
non terminal regalo, literal, llamada_funcion;
non terminal tokens, expresiones_AL, _expresiones_AL, _operadores_aritmeticos ;
non terminal expresiones, mas_expresiones;
non terminal condicional, condicional_if, condicionales_elif, condicional_elif, condicional_else;
non terminal ciclo_for, ciclo_do_until;
non terminal Symbol epsilon;

// ------------------------------ Start --------------------------------------------------------------------------------

start with navidad;

navidad ::= inicio {: AnalisisSemantico semantica = new AnalisisSemantico(arbol); :};

inicio ::= funcion inicio | funcion_main bolsa_navidenna;

bolsa_navidenna ::= funcion bolsa_navidenna | epsilon;
funcion ::=
	FUNCTION PERSONA:nf DOS_PUNTOS tipo:tf
	ABRECUENTO parametros CIERRECUENTO {: arbol.nuevasVariables(); :}
	bloque_codigo
	{: arbol.agregar(new Funcion(nf, tf, arbol.variables(true), arbol.bloqueCodigo())); :};

funcion_main ::=
	FUNCTION MAIN ABRECUENTO CIERRECUENTO
	bloque_codigo {: arbol.agregar(new Funcion(arbol.bloqueCodigo())); :};

parametros ::= parametro | parametro COMA mas_parametros | epsilon;
mas_parametros ::= parametro | parametro COMA mas_parametros;
parametro ::= PERSONA:np DOS_PUNTOS tipo:tp {: arbol.agregar(new Variable(np, tp)); :};

tipo ::=
	PAPA_NOEL:t {: RESULT = t; :} |
	SANTA_CLAUS:t {: RESULT = t; :} |
	SAN_NICOLAS:t {: RESULT = t; :} |
	NINO_JESUS:t {: RESULT = t; :} |
	COLACHO:t {: RESULT = t; :} |
	array:t {: RESULT = t; :};
array ::=
	PAPA_NOEL:t ABREEMPAQUE L_PAPA_NOEL:n CIERRAEMPAQUE {: RESULT = t + "[" + n + "]"; :} |
	NINO_JESUS:t ABREEMPAQUE L_PAPA_NOEL:n CIERRAEMPAQUE {: RESULT = t + "[" + n + "]"; :};

asignar_variable_declarada ::= PERSONA:nv {: RESULT = nv; :} ENTREGA regalo;
declarar_asignar_variable ::= parametro ENTREGA regalo;
asignar_variable ::=
	asignar_variable_declarada:nv {: RESULT = "asignar_variable_declarada|"+nv; :} |
	declarar_asignar_variable {: RESULT = "declarar_asignar_variable|null"; :};

linea_codigo ::=
	regalo {: arbol.agregar(LineaCodigo.expresion(arbol.expresiones())); :} FIN_REGALO | // expresión
	LOCAL parametros {: arbol.agregar(LineaCodigo.declaracionesVariables(arbol.variables())); :} FIN_REGALO | // declaración de una o varias variables
	asignar_variable_declarada:nv {: arbol.agregar(LineaCodigo.asignacionVariable(nv, arbol.expresiones())); :} FIN_REGALO | // asignación de variable ya declarada
	LOCAL declarar_asignar_variable {: arbol.agregar(LineaCodigo.declaracionAsignacionVariable(arbol.variables(), arbol.expresiones())); :} FIN_REGALO | // se declara y asigna variable
	condicional | // condicionales
	ENVIA regalo {: arbol.agregar(LineaCodigo.returnLine(arbol.expresiones())); :} FIN_REGALO | // return
	CORTA {: arbol.agregar(LineaCodigo.breakLine()); :} FIN_REGALO | // break
	ciclo_for |
	ciclo_do_until FIN_REGALO |
	NARRA ABRECUENTO regalo CIERRECUENTO FIN_REGALO; // print

codigo_multilinea ::= linea_codigo codigo_multilinea | epsilon;
codigo_contenido ::=
	{: arbol.agregar(LineaCodigo.bloqueCodigo()); :}
	ABREREGALO codigo_multilinea CIERRAREGALO;
bloque_codigo ::=
	{: arbol.agregar(LineaCodigo.bloqueCodigo()); :} linea_codigo |
	{: arbol.agregar(LineaCodigo.bloqueCodigo()); :} FIN_REGALO |
	codigo_contenido;

// ------------------------------ EXPRESIONES --------------------------------------------------------------------------------

regalo ::=
    literal |
    PERSONA:p {: arbol.agregar(Expresion.identificador(p)); :} |
    llamada_funcion |
    ESCUCHA ABRECUENTO CIERRECUENTO {: arbol.agregar(Expresion.read()); :};

literal ::=
	L_COLACHO:l {: arbol.agregar(Expresion.literal("float", l)); :} |
	L_SAN_NICOLAS:l {: arbol.agregar(Expresion.literal("string", l)); :} |
	L_PAPA_NOEL:l {: arbol.agregar(Expresion.literal("int", l)); :} |
	L_SANTA_CLAUS:l {: arbol.agregar(Expresion.literal("boolean", l)); :};

llamada_funcion ::=
	PERSONA:f ABRECUENTO {: arbol.nuevasExpresiones(); :} expresiones CIERRECUENTO
	{: arbol.agregar(Expresion.llamadaFuncion(f, arbol.expresiones())); :};

expresiones ::= regalo | regalo COMA mas_expresiones | epsilon;
mas_expresiones ::= regalo | regalo COMA mas_expresiones;

// ------------------------------ EXPRESIONES NUEVA --------------------------------------------------------------------------------
expresiones_AL ::= tokens _expresiones_AL
                    | llamada_funcion _expresiones_AL
                    | ABRECUENTO expresiones_AL CIERRECUENTO _expresiones_AL;

_expresiones_AL ::= _operadores_aritmeticos expresiones_AL | epsilon;

// ------------------------------ Símbolos -----------------------------------------------------------------------------
_operadores_aritmeticos ::= RUDOLPH | DASHER | COMET | VIXEN | TOTIN | RENILLO | GRINCH | QUIEN;

tokens ::=  PERSONA | L_COLACHO | L_SAN_NICOLAS | L_PAPA_NOEL ;

// ------------------------------ CONDICIONALES --------------------------------------------------------------------------------

// Añadir que se puedan declarar condicionales con solo una línea de código, no funciona con la línea de código por el for y por las condicionales, que son las producciones que no terminan con FIN_REGALO
condicional ::= condicional_if condicionales_elif;
condicionales_elif ::= condicional_elif condicionales_elif | condicional_else | epsilon;
condicional_if ::=
	{: arbol.nuevasExpresiones(); :} ELFO ABRECUENTO regalo CIERRECUENTO {: arbol.nuevasExpresiones(); :}
	codigo_contenido {: arbol.agregar(Condicional.ifLine(arbol.expresiones(true), arbol.bloqueCodigo())); :};
condicional_elif ::=
	{: arbol.nuevasExpresiones(); :} HADA ABRECUENTO regalo CIERRECUENTO {: arbol.nuevasExpresiones(); :}
	codigo_contenido {: arbol.agregar(Condicional.elif(arbol.expresiones(true), arbol.bloqueCodigo())); :};
condicional_else ::=
	DUENDE codigo_contenido
	{: arbol.agregar(Condicional.elseCondition(arbol.bloqueCodigo())); :};

// sustituir el primer regalo por una expresión lógica
// sustituir el segundo regalo por una acción cómo una operación aritmética o una llamada a función
ciclo_for ::=
	ENVUELVE ABRECUENTO {: arbol.nuevasVariables(); :} {: arbol.nuevasExpresiones(); :}
	asignar_variable:av {: arbol.nuevasVariables(); :} COMA regalo COMA regalo
	{: arbol.nuevasExpresiones(); :} CIERRECUENTO bloque_codigo
	{: arbol.agregar(LineaCodigo.forLine(av, arbol, arbol.expresiones(true), arbol.bloqueCodigo())); :};

// sustituir el regalo por una expresión lógica
ciclo_do_until ::=
	HACE bloque_codigo REVISA ABRECUENTO regalo CIERRECUENTO
	{: arbol.agregar(LineaCodigo.doUntil(arbol.bloqueCodigo(), arbol.expresiones())); :};

epsilon ::= {: RESULT = new Symbol(-1); :};
